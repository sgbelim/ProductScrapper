group 'com.sainsburys'
version '1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: "com.github.kt3k.coveralls"

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    }
}

archivesBaseName = 'sainsbury-product-scrapper'
mainClassName = "com.sainsburys.productscrapper.Application"
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

sourceSets {
    integrationTest {
        java {
            srcDir file('src/integrationTest/java')
        }
        resources {
            srcDir file('src/integrationTest/resources')
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter:1.3.5.RELEASE',
            'org.apache.commons:commons-lang3:3.2.1',
            'org.jsoup:jsoup:1.9.1',
            'com.fasterxml.jackson.core:jackson-core:2.7.4',
            'com.fasterxml.jackson.core:jackson-databind:2.7.4'

    testCompile 'junit:junit:4.12',
            'org.mockito:mockito-core:1.10.19',
            'org.assertj:assertj-core:3.4.1',
            'org.springframework.boot:spring-boot-starter-test:1.3.5.RELEASE',
            'net.javacrumbs.json-unit:json-unit:1.12.1',
            'net.javacrumbs.json-unit:json-unit-fluent:1.9.0'
}

checkstyle {
    toolVersion = "6.0"
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}

jacoco {
    toolVersion = "0.7.5.201505241946"
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect { fileTree(dir: it, exclude: ['**/*Constants**']) })
    }
}

task checkstyle(dependsOn: [checkstyleMain, checkstyleTest])

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'sainsbury-product-scrapper',
                'Implementation-Version': version,
                'Main-Class': 'com.sainsburys.productscrapper.Application'
    }
    baseName = 'sainsbury-' + project.name.toLowerCase() + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
